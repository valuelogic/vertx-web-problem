
buildscript {
    ext {
        vertxVersion = '3.5.0'
        problemVersion = '0.20.1'
    }
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

plugins {
  id 'net.researchgate.release' version '2.4.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'

repositories {
    jcenter()
}

group = 'one.valuelogic'
archivesBaseName = 'vertx-web-problem'


dependencies {
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"
    optional "io.vertx:vertx-rx-java:${vertxVersion}"
    optional "io.vertx:vertx-rx-java2:${vertxVersion}"
    compileOnly "io.vertx:vertx-codegen:${vertxVersion}:processor"
    compile "org.zalando:problem:${problemVersion}"
    compile "com.nurkiewicz.typeof:typeof:0.0.1"
    testCompile "org.zalando:jackson-datatype-problem:${problemVersion}"
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'io.rest-assured:rest-assured:3.0.3'
    testCompile "io.vertx:vertx-unit:${vertxVersion}"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'vertx-web-problem'
                packaging 'jar'
                artifactId 'vertx-web-problem'
                description 'vertx-web integration with zalando problem'
                url 'https://github.com/valuelogic/vertx-web-problem'

                scm {
                    connection 'scm:git:https://github.com/valuelogic/vertx-web-problem.git'
                    developerConnection 'scm:git:git@github.com:valuelogic/vertx-web-problem.git'
                    url 'https://github.com/valuelogic/vertx-web-problem'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'kgarlikowski'
                        name 'Konrad Garlikowski'
                        email 'kgarligowski@valuelogic.one'
                    }
                    developer {
                        id 'adomanski'
                        name 'Adam Domanski'
                        email 'adomanski@valuelogic.one'
                    }
                    developer {
                        id 'rpiotrowski'
                        name 'Rafa≈Ç Piotrowski'
                        email 'rpiotrowski@valuelogic.one'
                    }
                    developer {
                        id 'kfolwarczny'
                        name 'Krzysztof Folwarczny'
                        email 'kfolwarczny@valuelogic.one'
                    }
                }
            }
        }
    }
}


